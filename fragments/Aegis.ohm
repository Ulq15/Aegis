Aegis {
  Program         = classKey id ":" ClassBody endKey                                                     --declare
  ClassBody       = Declaration*
  Declaration     = VarDec ";"                                                                           --var
                  | FunDec                                                                               --func
  VarDec          = TypeExp Assignment                                                                   --initialize
                  | TypeExp id                                                                           --declare
                  | Assignment
  Assignment      = id "{" Comparand "}" "=" Exp                                                         --array
                  | id "ADD[" Exp "][" Exp "]"                                                           --dictAdd
                  | id "=" Exp                                                                           --assign
  FunDec          = id "(" (Param ("," Param)*)? ")" TypeExp? ":" Body endKey                            --declare
  Param           = TypeExp id                                                                           --single
  Body            = Statement*
  Statement       = Conditional 
                  | Loop
                  | returnKey Exp ";"                                                                    --return
                  | printKey "(" Exp ")" ";"                                                             --print
                  | Declaration
                  | Exp ";"                                                                              --expLine
                  | FunCall ";"                                                                          --funcLine
  FunCall         = id "(" (Exp ("," Exp)*)? ")"                                                         --call
  Exp             = Exp (logicop Formula)*                                                               --logic
                  | Formula
  Formula         = Formula (compareOp Comparand)*                                                       --compare
                  | Comparand
  Comparand       = Comparand (addop Term)*                                                              --arithmetic
                  | Term
  Term            = Term (multop Factor)*                                                                --multiOp
                  | Factor
  Factor          = Factor (exponentop Primary)*                                                         --exponent
                  | Primary                                                        
  Primary         = Primary crementOp                                                                    --postfix
                  | crementOp Primary                                                                    --prefix
                  | negateOp Primary                                                                     --negate
                  | "{" (Exp ("," Exp)*)? "}"                                                            --arrayLiteral
                  | id "{" Comparand "}"                                                                 --accessArray
                  | id "GET[" Exp "]"                                                                    --getDictionary
                  | "(" Exp ")"                                                                          --parens
                  | id                                                                                   --id
                  | literal                                                                              --literal
  Conditional     = "IF" "(" Exp "):" Body ("ELSEIF" "(" Exp "):" Body)* ("ELSE:" Body)? endKey          --condition
  Loop            = "DO" "(" VarDec "," Exp "," Exp "):" Body endKey                                     --stepByStep
                  | "LOOP" "(" Exp "):" Body endKey                                                      --statement
  crementOp       = "++" | "--"
  int             = digit+
  decimal         = digit+ ("." digit+)
  boolean         = "TRUE" | "FALSE"
  char            = alnum
                  | space
  stringLiteral   = "\"" char* "\""
  literal         = decimal | int | boolean | stringLiteral
  logicop         = "&" | "|"
  compareOp       = "==" | "!=" | ">=" | "<=" | "<" | ">" 
  negateOp        = "!"
  addop           = "+" | "-" 
  multop          = "*" | "/" | moduloKey
  exponentop      = "**"
  numType         = "NUM"
  decimalType     = "DECI"
  booleanType     = "BOOL"
  stringType      = "CHARS"
  TypeExp         = ArrayType | DictionaryType | literalType
  literalType     = numType | decimalType | booleanType | stringType
  ArrayType       = TypeExp "{" Comparand? "}" 
  DictionaryType  = "[" TypeExp "][" TypeExp "]" 
  moduloKey       = "MOD"
  conditionalKey  = "IF" | "ELSE" | "ELSEIF"
  loopKey         = "LOOP" | "DO"
  printKey        = "OUTPUT"
  endKey          = "END"
  returnKey       = "RETURN"
  classKey        = "CLASS"
  keyword         = literalType
                  | conditionalKey
                  | loopKey
                  | printKey
                  | endKey
                  | returnKey
                  | moduloKey
                  | classKey
  id              = ~keyword letter alnum*
  space          += "##" (~"\n" any)* ("\n" | end)                                                       --singleLineComment
                  | "#*" (~"*#" any)* ("*#" | end)                                                       --multiLineComment
}